(ns jasalt\bencode
  (:use Arokettu\Bencode\Bencode :as Bencode)
  (:use Arokettu\Bencode\Exceptions\InvalidArgumentException) # TODO
  (:use InvalidArgumentException)
  )

(defn valid-key?
  "Checks if `v` is a valid Bencode key or can be converted to a Bencode key."
  [v]
  (or (int? v) (float? v) (symbol? v) (keyword? v) (string? v)))

(declare phel->php [x])

(defn- phel-iterable->php [x]
  (let [arr (php/array)]
    (foreach [k v x]
      (when-not (valid-key? k)
        (throw (php/new InvalidArgumentException "Key can only be an integer, float, symbol, keyword or a string.")))
      (php/aset arr (phel->php k) (phel->php v)))
    arr))

(defn phel->php
  "Convert a Phel data type to a 'bencode compatible' value."
  [x]
  (cond
    (php/is_iterable x) (phel-iterable->php x)
    (symbol? x) (name x)
    (keyword? x) (name x)  # TODO should use str to keep keyword recognizable from strings (?)
    (float? x) (str x)     #      see Clojure for reference
    true x))

(defn encode
  "Returns the bencode representation of a value."
  [value]
  (when (php/is_resource value)
    (throw (php/new \InvalidArgumentException "Value can be any type except a resource.")))
  (php/:: Bencode (encode (phel->php value))))

(defn php->phel
  "Convert a php data structure to a 'phel compatible' value."
  [x]
  (cond
    (indexed? x) (for [v :in x] (php->phel v))
    (php-array? x) (let [hashmap (transient {})]
                     (foreach [k v x]
                       (put hashmap (keyword k) (php->phel v)))
                     (persistent hashmap))
    true x))

(defn decode
  "Decodes a bencode string."
  [s]
  (when-not (string? s) (throw (php/new InvalidArgumentException "Bencode must be a string.")))
  ## Calling decode keeping returned format as array, otherwise ArrayObjects are returned
  ## https://github.com/arokettu/bencode/blob/7bd5447c34846476822c01cb62c0765f94a89e8c/docs/decoding.rst#lists-and-dictionaries
  (let [php-data (php/:: Bencode # calls static method, not itself
                         (decode s
                                 (php-indexed-array) # opts
                                 (php/:: \Arokettu\Bencode\Bencode\Collection ARRAY) # listType
                                 (php/:: \Arokettu\Bencode\Bencode\Collection ARRAY)))] # dictType
    (php->phel php-data)))

## TODO streams support (?)
## https://sandfox.dev/php/bencode/decoding.html#working-with-streams

(comment

  ## Basic usage
  (encode [123 345 "asfd" {:foo (set :asdf 123 "FOO")}])
  (decode "li123ei345e4:asfdd3:fool4:asdfi123e3:FOOeee")

  (-> [123 345 "asfd" {:foo (set :asdf 123 "FOO")}]
      encode decode) # => [123 345 asfd {:foo [asdf 123 FOO]}]


  ## Miscallaneaous

  (php/json_decode "[123,345,\"asfd\",{\"foo\":[\"asdf\",123,\"FOO\"]}]" true 512 0)
  ## => <PHP-Array [123, 345, asfd, <PHP-Array [foo:<PHP-Array [asdf, 123, FOO]>]>]>

  (php/:: Bencode (decode "li123ei345e4:asfdd3:fool4:asdfi123e3:FOOeee"))
  ## => <PHP-Array [123, 345, asfd, Printer cannot print this type: ArrayObject]>

  ## Library returns ArrayObjects by default, requires setting return types:
  (php/:: Bencode (decode "li123ei345e4:asfdd3:fool4:asdfi123e3:FOOeee"
                          (php-indexed-array)
                          (php/:: \Arokettu\Bencode\Bencode\Collection ARRAY)
                          (php/:: \Arokettu\Bencode\Bencode\Collection ARRAY)))
  # => <PHP-Array [123, 345, asfd, <PHP-Array [foo:<PHP-Array [asdf, 123, FOO]>]>]>


  ## Testing original phel\json

  (require phel\json :as json)
  (json/encode [123 345 "asfd" {:foo (set :asdf 123 "FOO")}])
  (json/decode "[123,345,\"asfd\",{\"foo\":[\"asdf\",123,\"FOO\"]}]")
  ## (out): [123 345 asfd {:foo [asdf 123 FOO]}]

  ## hash-map key string becomes keyword
  (-> (json/decode "[123,345,\"asfd\",{\"foo\":[\"asdf\",123,\"FOO\"]}]")
      peek keys first type)  # => :keyword

  )
